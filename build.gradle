buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "gradle.plugin.se.patrikerdes:gradle-use-latest-versions-plugin:0.2.3"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.craigburke.gradle:client-dependencies:1.4.1'
    }
}

version "0.1.0"
group "famcation"

apply plugin: "io.spring.dependency-management"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'se.patrikerdes.use-latest-versions'
apply plugin: 'com.craigburke.client-dependencies'
apply plugin: "application"
apply plugin: "groovy"


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:bom:1.0.0.M4'
    }
}

dependencies {
    compile "io.micronaut:http-client"
    compile "io.micronaut.configuration:jdbc-hikari"
    compile "io.micronaut.configuration:hibernate-validator"
    compile "io.micronaut.configuration:hibernate-gorm"
    compile "io.micronaut:http-server-netty"
    compile "io.micronaut:runtime-groovy"
    compile "io.micronaut:views"
    compileOnly "io.micronaut:inject-groovy"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "com.h2database:h2:1.4.197"
    runtime "org.apache.tomcat:tomcat-jdbc:9.0.11"
    runtime "com.github.jknack:handlebars:4.1.0"
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "io.micronaut:inject-groovy"
}

clientDependencies {
    installDir = 'src/main/resources/public/deps'
    yarn {
        'bootstrap'('4.1.3', transitive: false) {
            include 'css/bootstrap.css', 'js/bootstrap.js'
        }
        'jquery'('3.3.1') {
            include 'jquery.slim.js'
        }
        'popper.js'('1.14.4') {
            include 'umd/popper.js'
        }
        'font-awesome'('4.7.0') {
            include 'css/font-awesome.css', 'fonts/**'
        }
        'moment-countdown'('0.0.3', transitive: false)
        'moment'('2.22.2') {
            include 'moment.js'
        }
        'countdown'('2.6.0') {
            include 'countdown.js'
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "famcation.FamcationApplication"

task stage() {
    dependsOn clean, shadowJar
}

shadowJar.mustRunAfter clean
